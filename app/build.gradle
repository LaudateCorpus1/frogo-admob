apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 1

    def appDomain = "com"
    def appDevConsole = "frogobox"
    def appName = "admobsample"

    def projectApplicationId = "$appDomain.$appDevConsole.$appName"
    def projectVersionCode = (versionMajor * 100) + (versionMinor * 10) + (versionPatch * 1)
    def projectVersionName = "$versionMajor.$versionMinor.$versionPatch"

    // Declaration app name
    def nameApp = "Frogo Admob Helper"
    def nameAppLower = nameApp.toLowerCase()

    // Declaration apk / aab name
    def nameAPK = nameAppLower.replace(" ", "-")

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId projectApplicationId
        minSdkVersion 21
        targetSdkVersion 30
        versionCode projectVersionCode
        versionName projectVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Declaration admob id for debug
        def debugAdmobPublisherId = "ca-app-pub-3940256099942544~3347511713"
        def debugAdmobBanner = "ca-app-pub-3940256099942544/6300978111"
        def debugAdmobInterstitial = "ca-app-pub-3940256099942544/1033173712"
        def debugAdmobInterstitialVideo = "ca-app-pub-3940256099942544/8691691433"
        def debugAdmobRewardedVideo = "ca-app-pub-3940256099942544/5224354917"
        def debugAdmobNativeAdvanced = "ca-app-pub-3940256099942544/2247696110"
        def debugAdmobNativeAdvancedVideo = "ca-app-pub-3940256099942544/1044960115"

        // Naming APK // AAB
        setProperty("archivesBaseName", "${nameAPK}(${versionName})#${gitCurrentBranch()}")

        resConfigs "en", "id"
        vectorDrawables.useSupportLibrary = true

        // Inject app name for debug
        resValue "string", "app_name", nameApp

        // Inject admob id for debug
        resValue "string", "admob_publisher_id", debugAdmobPublisherId
        resValue "string", "admob_banner", debugAdmobBanner
        resValue "string", "admob_interstitial", debugAdmobInterstitial
        resValue "string", "admob_interstitial_video", debugAdmobInterstitialVideo
        resValue "string", "admob_rewarded_video", debugAdmobRewardedVideo
        resValue "string", "admob_native_advanced", debugAdmobNativeAdvanced
        resValue "string", "admob_native_advanced_video", debugAdmobNativeAdvancedVideo

    }

    signingConfigs {
        release {
            // You need to specify either an absolute path or include the
            // keystore file in the same directory as the build.gradle file.
            // [PROJECT FOLDER NAME/app/[COPY YOUT KEY STORE] .jks in here
            storeFile file("key.jks")
            storePassword "key"
            keyAlias "key"
            keyPassword "key"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Generated Signed APK / AAB
            signingConfig signingConfigs.release

            // Declaration admob id for release
            def releaseAdmobPublisherId = "ca-app-pub-3940256099942544~3347511713"
            def releaseAdmobBanner = "ca-app-pub-3940256099942544/6300978111"
            def releaseAdmobInterstitial = "ca-app-pub-3940256099942544/1033173712"
            def releaseAdmobInterstitialVideo = "ca-app-pub-3940256099942544/8691691433"
            def releaseAdmobRewardedVideo = "ca-app-pub-3940256099942544/5224354917"
            def releaseAdmobNativeAdvanced = "ca-app-pub-3940256099942544/2247696110"
            def releaseAdmobNativeAdvancedVideo = "ca-app-pub-3940256099942544/1044960115"

            // Inject app name for release
            resValue "string", "app_name", nameApp

            // Inject admob id for release
            resValue "string", "admob_publisher_id", releaseAdmobPublisherId
            resValue "string", "admob_banner", releaseAdmobBanner
            resValue "string", "admob_interstitial", releaseAdmobInterstitial
            resValue "string", "admob_interstitial_video", releaseAdmobInterstitialVideo
            resValue "string", "admob_rewarded_video", releaseAdmobRewardedVideo
            resValue "string", "admob_native_advanced", releaseAdmobNativeAdvanced
            resValue "string", "admob_native_advanced_video", releaseAdmobNativeAdvancedVideo

        }
    }
}

static def gitCurrentBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation project(':frogoadmob')

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.material:material:1.4.0-alpha02'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // lib google ads
    implementation 'com.google.android.gms:play-services-ads:20.1.0'

    // library frogo-log
    implementation 'com.github.amirisback:frogo-log:2.0.0'

    // library frogo-ui-kit
    implementation 'com.github.amirisback:frogo-ui-kit:1.0.5'

    // library frogo-recycler-view
    implementation 'com.github.amirisback:frogo-recycler-view:3.5.0'

    // library consumable code movie tmdb api
    implementation 'com.github.amirisback:consumable-code-movie-tmdb-api:1.1.5'

    // library consumable code news api
    implementation 'com.github.amirisback:consumable-code-news-api:1.0.5'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
